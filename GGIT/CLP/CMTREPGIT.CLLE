pgm parm(&repert &text) /*-------------------------------------------*/
/* Ce programme commit les modifications d'un repository GIT         */
/*-------------------------------------------------------------------*/
/* Paramètres */
dcl &repert *char 30
dcl &text *char 50
/* Variables */
dcl &cmdsh *char 500
dcl &config *char 20
dcl &dat *char 6
dcl &fil *char 100
dcl &wfil *char 100
dcl &job *char 10
dcl &usr *char 10
/* traitemnt des paramètres */
if cond(&text = '*DFT') then(do)
rtvsysval qdate &dat
chgvar &text ('Application' *bcat &repert *bcat &dat)
enddo
             RTVJOBA    JOB(&JOB) USER(&USR)
             CHGVAR     VAR(&FIL) VALUE('/gitlog/' *TCAT &JOB +
                          *TCAT '_' *TCAT &USR *TCAT '_lsout.txt')
             CHGVAR     VAR(&wFIL) VALUE('FILE=' *tcat &fil)
 /* File for STDOUT           */
             ADDENVVAR  ENVVAR(QIBM_QSH_CMD_OUTPUT) VALUE(&WFIL)
             MONMSG     MSGID(CPFA980) exec(DO)
             CHGENVVAR  ENVVAR(QIBM_QSH_CMD_OUTPUT) VALUE(&WFIL)
             enddo
 /* msg escape on error       */
             ADDENVVAR  ENVVAR(QIBM_QSH_CMD_ESCAPE_MSG) value('Y')
             MONMSG     MSGID(CPFA980) exec(DO)
             CHGENVVAR  ENVVAR(QIBM_QSH_CMD_ESCAPE_MSG) value('Y')
             enddo
 /* force to use prestart job */
             ADDENVVAR  ENVVAR(QSH_USE_PRESTART_JOB) value('Y')
             MONMSG     MSGID(CPFA980) exec(DO)
             CHGENVVAR  ENVVAR(QSH_USE_PRESTART_JOB) value('Y')
             enddo
/* nom du fichier de configuration du GIT */
chgvar &config (&repert *tcat '/.git/config')
             CHKout     OBJ(&CONFIG)
monmsg CPFA000 exec(do)
             SNDUSRMSG  MSG('Repository, ' *BCAT &REPERT *BCAT +
                          'inexistante') MSGTYPE(*INFO)
return
enddo
             CHKin      OBJ(&CONFIG)
/* commit   repository */
chgvar &cmdsh (+
'cd ' *bcat &repert  *bcat ' ; ' *bcat +
'git add *.*  ; ' *bcat +
'git commit -m "' *tcat &text *tcat '"')
             STRQSH     CMD(&CMDSH)
             monmsg QSH0000  exec(do)
             SNDUSRMSG  MSG('Commit en erreur, consultez le fichier ' +
                          *BCAT &FIL) MSGTYPE(*INFO)
             enddo
fin:
endpgm
